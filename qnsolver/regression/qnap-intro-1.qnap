& $Id: qnap-intro-1.qnap 16156 2022-12-01 20:57:10Z greg $
&
& First model from QNAP intro from section 3.2.3.
& ------------------------------------------------------------------------
/declare/                               & declaration statement
queue   real t;
integer n_disk=2;                       & number of disk units
real    trans(n_disk);                  & visit ratios to disk units
queue   cpu,                            & central processing unit
        terminal,                       & set of terminals
        disk(n_disk);                   & disk units
integer nb_term;                        & number of termainals
&
/station/
        name=cpu;
        sched=ps;
        service=hexp(t,5.);
        transit=disk(1 step 1 until n_disk), trans(1 step 1 until n_disk),
                terminal, 1;
/station/
        name=disk(1 step 1 until n_disk);
        service=exp(t);
        transit=cpu;
/station/
        name=terminal;
        type=infinite;
        service=exp(t);
        init=nb_term;
        transit=cpu;
/declare/
        ref queue r_q;
/control/
	entry = begin
        print(" ");
        print("input parameters");
        print(" ");
        print("number of terminals       : ",nb_term );
        for r_q := all queue do
          print("mean service time at ",r_q," : ",r_q.t);
        end;
	exit = begin
	print(" ");
	print("results");
	print(" ");
	print("mean response time of trans. ",
	      nb_term/mthruput(terminal)-terminal.t);
	end;
	option=nresult;
/exec/  begin
        nb_term:=5;
        cpu.t:=0.2;
        disk(1).t:=0.08; disk(2).t:=0.1;
        trans:=3,6;
        terminal.t:=30.;
        solve;
        end;
/end/
